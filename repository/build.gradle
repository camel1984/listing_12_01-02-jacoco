apply from: 'databaseSetup.gradle'

repositories {
    maven {
        url "https://repo1.maven.org/maven2/"
    }

    maven {
        url "https://repo.spring.io/plugins-release/"
    }
}

dependencies {
    compile project(':model')
    runtime 'com.h2database:h2:1.3.170'
    testCompile 'junit:junit:4.11'
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    dependsOn startAndPrepareDatabase
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

task integrationTestReport (type: JacocoReport) {

    executionData project.tasks.integrationTest
    def reportDir = project.reporting.file("jacoco/integrationTest/html")
//    def reportDir = project.reporting.file("jacoco/test/html")

    sourceDirectories = project.files(project.sourceSets.main.allSource.srcDirs)
    classDirectories = project.sourceSets.main.output

    reports {
        html.destination = reportDir
    }
    doLast {
        println "See report at: file://${reportDir.toURI().path}index.html"
    }
}

integrationTestReport.dependsOn integrationTest

integrationTest.dependsOn jacocoTestReport

jacocoTestReport.dependsOn test

check.dependsOn integrationTestReport

